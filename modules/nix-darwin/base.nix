{
  config,
  bayt-lib,
  lib,
  options,
  pkgs,
  ...
}: let
  inherit (builtins) attrNames attrValues concatLists concatMap filter getAttr head isAttrs toJSON;
  inherit (bayt-lib) fileToJson;
  inherit (lib.attrsets) filterAttrs;
  inherit (lib.meta) getExe getExe';
  inherit (lib.modules) importApply mkAfter mkDefault;
  inherit (lib.strings) concatMapAttrsStringSep escapeShellArgs;
  inherit (lib.trivial) flip pipe;
  inherit (lib.types) submoduleWith;

  cfg = config.bayt;
  _class = "darwin";

  enabledUsers = filterAttrs (_: u: u.enable) cfg.users;

  userFiles = user: [
    user.files
    user.xdg.cache.files
    user.xdg.config.files
    user.xdg.data.files
    user.xdg.state.files
  ];

  linker = getExe cfg.linker;

  newManifests = let
    writeManifest = user: let
      name = "manifest-${user.name}.json";
    in
      pkgs.writeTextFile {
        inherit name;
        destination = "/${name}";
        text = toJSON {
          version = 3;
          files = concatMap (
            flip pipe [
              attrValues
              (filter (x: x.enable))
              (map fileToJson)
            ]
          ) (userFiles user);
        };
        checkPhase = ''
          set -e
          CUE_CACHE_DIR=$(pwd)/.cache
          CUE_CONFIG_DIR=$(pwd)/.config

          ${getExe pkgs.cue} vet -c ${../../manifest/v3.cue} $target
        '';
      };
  in
    pkgs.symlinkJoin
    {
      name = "bayt-manifests";
      paths = map writeManifest (attrValues enabledUsers);
    };

  baytSubmodule = submoduleWith {
    description = "Bayt submodule for nix-darwin";
    class = "bayt";
    specialArgs =
      cfg.specialArgs
      // {
        inherit bayt-lib pkgs;
        osConfig = config;
        osOptions = options;
      };
    modules =
      concatLists
      [
        [
          ../common/user.nix
          ({name, ...}: let
            user = getAttr name config.users.users;
          in {
            name = mkDefault user.name;
            user = mkDefault user.name;
            directory = mkDefault user.home;
            clobberFiles = mkDefault cfg.clobberByDefault;
          })
        ]
        # Evaluate additional modules under 'bayt.users.<username>' so that
        # module systems built on Bayt are more ergonomic.
        cfg.extraModules
      ];
  };

  linkerArgs =
    if isAttrs cfg.linkerOptions
    then let
      f = pkgs.writeText "smfh-opts.json" (toJSON cfg.linkerOptions);
    in ["--linker-opts" f]
    else cfg.linkerOptions;

  argsStr = escapeShellArgs linkerArgs;
in {
  imports = [
    (importApply ../common/top-level.nix {inherit baytSubmodule _class;})
  ];

  config = {
    # Temporary requirement: choose a primary user, pick the first enabled user.
    # This option will be deprecated in the future.
    system.primaryUser = mkDefault (head (attrValues enabledUsers)).name;

    # launchd agent to apply/diff the manifest per logged-in user
    # https://github.com/nix-darwin/nix-darwin/issues/871#issuecomment-2340443820
    launchd.user.agents = {
      bayt-activate = {
        serviceConfig = {
          Program = getExe (pkgs.writeShellApplication {
            name = "bayt-activate";
            # Maybe the kickstart is broken because a runtimeInput is missing?
            runtimeInputs = with pkgs; [coreutils-full bash];
            text = ''
              set -euo pipefail

              USER="$(id -un)"
              NEW="${newManifests}/manifest-''${USER}.json"

              if [ ! -f "$NEW" ]; then
                exit 0
              fi

              STATE_DIR="$HOME/Library/Application Support/Bayt"
              mkdir -p "$STATE_DIR"
              CUR="$STATE_DIR/manifest.json"

              if [ ! -f "$CUR" ]; then
                ${linker} ${argsStr} activate "$NEW"
              else
                ${linker} ${argsStr} diff "$NEW" "$CUR"
              fi

              cp -f "$NEW" "$CUR"
            '';
          });
          Label = "org.bayt.activate";
          RunAtLoad = true;
          StandardOutPath = "/var/tmp/bayt-activate.out";
          StandardErrorPath = "/var/tmp/bayt-activate.err";
        };
      };

      # Currently forced upon users, perhaps we should make an option for enabling this behavior?
      # Leaving it be for now.
      link-nix-apps = {
        serviceConfig = {
          Program = getExe (pkgs.writeShellApplication {
            name = "link-nix-apps";
            runtimeInputs = with pkgs; [coreutils-full findutils gnugrep nix];
            text = ''
              set -euo pipefail

              USER="$(id -un)"
              GROUP="$(id -gn)"
              DEST="$HOME/Applications/Nix User Apps"
              PROFILE="/etc/profiles/per-user/$USER"

              install -d -m 0755 -o "$USER" -g "$GROUP" "$DEST"

              desired="$(mktemp -t desired-apps.XXXXXX)"
              trap 'rm -f "$desired"' EXIT

              nix-store -qR "$PROFILE" | while IFS= read -r p; do
                apps="$p/Applications"
                if [ -d "$apps" ]; then
                  find "$apps" -maxdepth 1 -type d -name "*.app" -print0 \
                  | while IFS= read -r -d "" app; do
                      bname="$(basename "$app")"
                      echo "$bname" >> "$desired"
                      ln -sfn "$app" "$DEST/$bname"
                    done
                fi
              done

              sort -u "$desired" -o "$desired"

              find "$DEST" -maxdepth 1 -type l -name "*.app" -print0 \
              | while IFS= read -r -d "" link; do
                  name="$(basename "$link")"
                  if ! grep -Fxq "$name" "$desired"; then
                    rm -f "$link"
                  fi
                done

              # Remove broken links
              find "$DEST" -maxdepth 1 -type l -name "*.app" -print0 \
                | xargs -0 -I {} bash -c '[[ -e "{}" ]] || rm -f "{}"'
            '';
          });
          Label = "org.nix.link-nix-apps";
          RunAtLoad = true;
          StandardOutPath = "/var/tmp/link-nix-apps.out";
          StandardErrorPath = "/var/tmp/link-nix-apps.err";
        };
      };
    };

    system.activationScripts = {
      bayt-activate-kick.text = mkAfter (
        concatMapAttrsStringSep "\n"
        (u: _: ''
          if uid="$(${getExe' pkgs.coreutils-full "id"} -u ${u} 2>/dev/null)"; then
            /bin/launchctl kickstart -k "gui/''${uid}/${config.launchd.user.agents.bayt-activate.serviceConfig.Label}" 2>/dev/null || true
          fi
        '')
        enabledUsers
      );

      # Kick the user agent for every configured user at activation.
      applications.text = mkAfter (
        concatMapAttrsStringSep "\n"
        (u: _: ''
          if uid="$(${getExe' pkgs.coreutils-full "id"} -u ${u} 2>/dev/null)"; then
            /bin/launchctl kickstart -k "gui/''${uid}/${config.launchd.user.agents.link-nix-apps.serviceConfig.Label}" 2>/dev/null || true
          fi
        '')
        enabledUsers
      );
    };
  };
}
