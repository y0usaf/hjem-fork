{
  baytModule,
  baytTest,
  lib,
  formats,
  writeText,
}: let
  userHome = "/home/alice";
in
  baytTest {
    name = "bayt-xdg";
    nodes = {
      node1 = {
        imports = [baytModule];

        users.groups.alice = {};
        users.users.alice = {
          isNormalUser = true;
          home = userHome;
          password = "";
        };

        bayt.linker = null;
        bayt.users = {
          alice = {
            enable = true;
            files = {
              "foo" = {
                text = "Hello world!";
              };
            };
            xdg = {
              cache = {
                directory = userHome + "/customCacheDirectory";
                files = {
                  "foo" = {
                    text = "Hello world!";
                  };
                };
              };
              config = {
                directory = userHome + "/customConfigDirectory";
                files = {
                  "bar.json" = {
                    generator = lib.generators.toJSON {};
                    value = {bar = "Hello second world!";};
                  };
                };
              };
              data = {
                directory = userHome + "/customDataDirectory";
                files = {
                  "baz.toml" = {
                    generator = (formats.toml {}).generate "baz.toml";
                    value = {baz = "Hello third world!";};
                  };
                };
              };
              state = {
                directory = userHome + "/customStateDirectory";
                files = {
                  "foo" = {
                    source = writeText "file-bar" "Hello fourth world!";
                  };
                };
              };

              mime-apps = {
                added-associations."text/html" = ["firefox.desktop" "zen.desktop"];
                removed-associations."text/xml" = ["thunderbird.desktop"];
                default-applications."text/html" = "firefox.desktop";
              };
            };
          };
        };

        # Also test systemd-tmpfiles internally
        systemd.user.tmpfiles = {
          rules = [
            "d %h/user_tmpfiles_created"
          ];

          users.alice.rules = [
            "d %h/only_alice"
          ];
        };
      };
    };

    testScript = ''
      machine.succeed("loginctl enable-linger alice")
      machine.wait_until_succeeds("systemctl --user --machine=alice@ is-active systemd-tmpfiles-setup.service")

      # Test XDG files created by Bayt
      with subtest("XDG basedir spec files created by Bayt"):
        machine.succeed("[ -L ~alice/customCacheDirectory/foo ]")
        machine.succeed("grep \"Hello world!\" ~alice/customCacheDirectory/foo")
        machine.succeed("[ -L ~alice/customConfigDirectory/bar.json ]")
        machine.succeed("grep \"Hello second world!\" ~alice/customConfigDirectory/bar.json")
        machine.succeed("[ -L ~alice/customDataDirectory/baz.toml ]")
        machine.succeed("grep \"Hello third world!\" ~alice/customDataDirectory/baz.toml")
        # Same name as config test file to verify proper merging
        machine.succeed("[ -L ~alice/customStateDirectory/foo ]")
        machine.succeed("grep \"Hello fourth world!\" ~alice/customStateDirectory/foo")

      with subtest("XDG mime-apps spec file created by Bayt"):
        machine.succeed("[ -L ~alice/customConfigDirectory/mimeapps.list ]")
        machine.succeed("grep \"text/xml\" ~alice/customConfigDirectory/mimeapps.list")

      with subtest("Basic test file for Bayt"):
        machine.succeed("[ -L ~alice/foo ]") # Same name as cache test file to verify proper merging
        machine.succeed("grep \"Hello world!\" ~alice/foo")
        # Test regular files, created by systemd-tmpfiles
        machine.succeed("[ -d ~alice/user_tmpfiles_created ]")
        machine.succeed("[ -d ~alice/only_alice ]")
    '';
  }
